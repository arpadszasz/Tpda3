#!/usr/bin/perl

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;

use TpdaMvc;
use TpdaMvc::Config;

# Parse options and print usage if there is a syntax error,
# or if usage was explicitly requested.
my $help    = q{};
my $man     = q{};
my $version;
my $list;
my $init    = q{};
my $user;
my $pass;

# Process options.
if ( @ARGV > 0 ) {
    GetOptions(
        'help|?'    => \$help,
        'man'       => \$man,
        'version'   => \$version,
        'list'      => \$list,
        'user=s'    => \$user,
        'password=s'=> \$pass,
        'init:s'    => \$init,
        ),
        or pod2usage(2);
}
if ( $man or $help or $version or $#ARGV >= 0 ) {
    pod2usage( VERBOSE => 2 ) if $man;
    if ($version) { tpda_mvc_version(); exit 0; }
    if ($help)    { tpda_mvc_version(); pod2usage(1); }
}

my $cfgname = shift;         # Runtime configuration name or database name

#- Options
my $opts = {};

# Check 'list' and 'init' options first
if ( defined $list xor $init ) {

    # Initialize configurations
    my $cfg = TpdaMvc::Config->instance();

    if ( $list ) {
        $cfg->list_configs();
        exit 0;
    }
    if ( $init ) {
        $cfg->init_configs($init);
        exit 0;
    }
}

# Check connection configuration name option last
if ( $cfgname ) {

    # Other options
    $opts->{user} = $user;
    $opts->{pass} = $pass;
    $opts->{cfgname} = $cfgname;

    # Go and run the app
    TpdaMvc->new( $opts )->run;

    print "Normal exit.\n";
    exit 0;
}

# If no other option, usage
tpda_mvc_version();
pod2usage(1);

#- Print version

sub tpda_mvc_version {

    my $ver = $TpdaMvc::VERSION;
    print "TPDA - Query Repository Tool v$ver\n(C) 2010 Stefan Suciu\n\n";
}

__END__

=head1 NAME

tpda_mvc - TPDA - Query Repository Tool

=head1 SYNOPSIS

tpda_mvc -list

tpda_mvc -init <connection>

tpda_mvc <connection> [-user <user> [-password <pass>]]

Options:

 -init <connection>   Initialize (create) configuration path
 -list                List available configuration names
 -user                User name
 -password            Password
 -help                Brief help message
 -man                 Full documentation
 -version             Current version

=head1 DESCRIPTION

TPDA - Query Repository Tool - a wxPerl GUI tool for data exporting
and SQL query repository management. Queries are saved in XML files
and can be edited and parametrized.

=head2 Configuration

A configuration name is the name of the directory underneath the
B<.tpda_mvc/db/> path.  This directory contains two other directories
B<etc> and B<qdf>. The B<etc> directory contains a file named
B<connection.yml>.  This file has to be updated with the connection
configuration for your database.  The other directory named B<qdf> is
the place where your query definition files are stored.  This files
can be created and updated with the GUI or can be copied from existing
repositories.

Configuration files are in YAML format, the format described in the
documentation of the YAML::Tiny module.

=head2 Quick start

After installing the application, at the first run the user
configuration tree in the user's home directory is created if it
doesn't exists.

Next, run B<tpda_mvc> with the b<-init> option to create a new
configuration for the connection and query definition files.

Last step is to run B<tpda_mvc> with the connection name created at
the previous step as argument, create new B<qdf> files and use them to
export data from the tables in the formats currently supported.

Have fun!

=head1 OPTIONS DETAILS

=over

=item B<-init> <connection>

Initialize (create) configuration paths and created default
configuration files from template.  A connection name is required.

=item B<-list>

List all configuration names.  Usually a configuration name is the
same as the database name, but this is not a requirement.

=item B<-user>

Sets the user name.  If the user is specified but not the password than
the login dialog will ask only for the password.  If both the password
and user are provided then the login dialog will be skipped.

=item B<-password>

Sets the password.  If the user option is not specified than show
usage and exit.  If the user is specified skip the login dialog.

=item B<-man>

Prints the manual page and exits.

=item B<-version>

Print current version information.

=back

=head1 LICENSE

Copyright (C) 2010  Stefan Suciu
This program is free software, you can redistribute it and/or
modify it under the terms of GNU General Public License.

=head1 AUTHOR

Stefan Suciu <stefansbv 'at' users . sourceforge . net>

=cut
